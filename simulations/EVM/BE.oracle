// $qucf BE ./ 

CONSTANTS
	nx 4
	nv 4
	Nvhm1 7
	Nxm2 14
	Nxm4 12

	Nvm1 15
	Nvm3 13
	Nvm4 12

	//--- Parameters for the submatrix Cf ---
	alpha_0_cf 	-2.841788633538e-02
	alpha_1_cf 	3.031241209107e-02

	//--- Parameters for the oracle for the submatrix CE ---
	alpha_0_CE 	-1.000000000000e+00
	alpha_1_CE 	1.066666666667e+00

	//--- Parameters for the submatrix S ---
	angle_sb 	-0.002131341879
	angle_se 	-0.002131344299

	//--- Parameters for the submatrix F-prof ---

	//- diag 0: left edge -
	a0_FB0_L 	-0.018367610748
	a1_FB0_L 	0.009645350465
	a0_FB0_iw_L 	-0.048227920803

	//- diag 0: left edge: corrections -
	aL_inv_v0  		-0.036735221496
	aL_corr_v0_r 	3.191950916866
	aL_corr_v0_i 	-0.068216153103

	aL_inv_v2  		-0.027089871032
	aL_corr_v2_r 	3.179905859897
	aL_corr_v2_i 	-0.068216153103

	aL_inv_v3  		-0.022267195799
	aL_corr_v3_r 	3.173084525189
	aL_corr_v3_i 	-0.068216153103

	//- diag 0: right edge -
	a0_FB0_R 	-0.002104699707
	a1_FB0_R 	-0.013640585441
	a0_FB0_iw_R 	-0.068206232381

	//- diag 0: right edge: corrections -
	aR_inv_vm1  	-0.051951448457
	aR_corr_vm1_r 	3.242341142183
	aR_corr_vm1_i 	-0.136511828193
	
	aR_inv_vm3  	-0.038310863016
	aR_corr_vm3_r 	3.203339949591
	aR_corr_vm3_i 	-0.109963577703
	
	aR_inv_vm4  	-0.031490570295
	aR_corr_vm4_r 	3.186130726243
	aR_corr_vm4_i 	-0.096490932742

	//- diag +-1 -
	alpha_0_LFB1 	-0.002131341475
	alpha_1_LFB1 	0.002273430907

	alpha_0_LFB1_corr1 	-3.014172020211e-03
	alpha_1_LFB1_corr1 	1.607558410779e-03
	alpha_0_LFB1_corr1_2 	1.420894316769e-04
	alpha_1_LFB1_corr1_2 	1.136715453415e-03

	alpha_0_LFB1_corr2 	-2.131341475154e-03
	alpha_1_LFB1_corr2 	1.136715453415e-03
	alpha_0_LFB1_corr2_2 	-2.841788633538e-04
	alpha_1_LFB1_corr2_2 	-2.273430906831e-03
	
	alpha_0_LFB1_corr3 	4.018896026947e-04
	alpha_1_LFB1_corr3 	3.215116821558e-03

	//- diag +1 -
	alpha_0_RFB1 	0.002131341475
	alpha_1_RFB1 	-0.002273430907

	alpha_0_RFB1_corr1 	6.028344040421e-03
	alpha_1_RFB1_corr1 	-3.215116821558e-03

	alpha_0_RFB1_corr2 	3.014172020211e-03
	alpha_1_RFB1_corr2 	-1.607558410779e-03
	alpha_0_RFB1_corr2_2 	-1.420894316769e-04
	alpha_1_RFB1_corr2_2 	-1.136715453415e-03

	alpha_0_RFB1_corr3 	2.131341475154e-03
	alpha_1_RFB1_corr3 	-1.136715453415e-03
	alpha_0_RFB1_corr3_2 	-6.860684660486e-04
	alpha_1_RFB1_corr3_2 	-5.488547728388e-03

	alpha_0_RFB1_corr4 	2.131341475154e-03
	alpha_1_RFB1_corr4 	-1.160496613989e-03
	alpha_0_RFB1_corr4_2 	-1.004724006737e-04
	alpha_1_RFB1_corr4_2 	-8.037792053895e-04

	//- diag +-2 -
	alpha_0_FL2 	-0.002131341475
	alpha_1_FL2 	0.002273430907
	alpha_0_FR2 	-0.004262682950
	alpha_1_FR2 	0.004546861814
	pi2 	6.283185307180
END_CONSTANTS


OPTIONS
	sel_compute_output all // none, all, zero-ancillae
	sel_print_output   all // none, all, zero-ancillae
	flag_circuit 0
	flag_tex     0
	tex_CL  16 

	flag_matrix 1 
END_OPTIONS

//  2*nv + nx + 11
CIRCUITS_DECLARATION
	Shpm  1 ar 3 1
	Shpp  1 ar 3 1
	Shppp 1 ar 3 1
	Shmm  1 ar 3 1
	Shmmm 1 ar 3 1

	U_BE_CE  2  a_ce 1 1 rv <nv> 0

	OF_VAR  7        af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0
	OF_XB   7        af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0
	OFN_XE  7        af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0
	OF_VB   7        af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0
	OFN_VE  7        af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0
	OM      7        af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0
	OFNI_XE 7        af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0
	OFNI_VE 7        af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0

	OH    10 ae 1 1 a_lcu 1 1 a_ce 1 1 af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0

	U_BE  10 ae 1 1 a_lcu 1 1 a_ce 1 1 af 1 1 axr 3 1 avr 3 1 av <nv> 1 rf 1 0 rx <nx> 0 rv <nv> 0
END_CIRCUITS_DECLARATION



MAIN_CIRCUIT   U_BE       
	// INPUT_STATE  3 rf 0 rx 0 rv 0
END_MAIN_CIRCUIT


CIRCUIT_STRUCTURE Shpm
	gate H ar[0]               end_gate
	gate H ar[2] control ar[0] end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE Shpp
	gate H ar[2] control ar[0] end_gate
	//---
	gate H ar[1] ocontrol ar[0] end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE Shppp
	gate H ar[2] control ar[0] end_gate
	//---
	gate H ar[1] end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE Shmm
	circuit Shpp -1 end_circuit
	gate X ar[2] end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE Shmmm
	circuit Shppp -1 end_circuit
	gate X ar[2] end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE OM
	// *** matrices CE and Cf ***
	gate swap  av -1 rv -1 control af 1 ocontrol rf 1  end_gate
	gate swap  av -1 rv -1 control rf 1 ocontrol af 1  end_gate
	//*** variables' subblocks ---
	gate swap af -1 rf -1 end_gate
	//*** matrix F: spatial subblocks ***
	gate adder1  rx -1 control_e axr 1 end_gate
	gate adder2  rx -1 control_e axr 2 end_gate
	igate adder1 rx -1 ocontrol_e axr 2 end_gate
	igate adder2 rx -1 ocontrol_e axr 1 end_gate
	//*** matrix F: velocity subblocks ***
	gate adder1  rv -1 control_e avr 1 end_gate
	gate adder2  rv -1 control_e avr 2 end_gate
	gate adder3  rv -1 control_e avr 3 end_gate
	igate adder1 rv -1 ocontrol_e avr 2 end_gate
	igate adder2 rv -1 ocontrol_e avr 1 end_gate
	igate adder3 rv -1 ocontrol_e avr 0 end_gate
END_CIRCUIT_STRUCTURE



CIRCUIT_STRUCTURE OF_VAR
	// *** create subblocks corresponding to two variables ***
	gate H af 1              ocontrol rf 1  end_gate
	gate X af 1 control rf 1                end_gate
	gate H af 1 control rf 1 ocontrol rv -1 end_gate
END_CIRCUIT_STRUCTURE


// *** matrix F: axis x: bulk points ***
CIRCUIT_STRUCTURE OF_XB
	// --- x: bulk spatial points:
	circuit Shpm 1 axr -1                 ocontrol 2 rf 1 af 1        end_circuit
END_CIRCUIT_STRUCTURE


// *** matrix F: axis x: edge points ***
CIRCUIT_STRUCTURE OFN_XE
	// left x boundary: v > 0:
	icircuit Shpm 1 axr -1 control rv[-1] ocontrol 3 rf 1 af 1 rx -1  end_circuit 
	// right x boundary: v < 0:
	icircuit Shpm 1 axr -1 control rx -1  ocontrol 3 rf 1 af 1 rv[-1] end_circuit	
	// left x boundary: v < 0:
	circuit Shpp 1 axr -1                        ocontrol 4 rf 1 af 1 rx -1 rv[-1] end_circuit
	// right x boundary: v > 0:
	circuit Shmm 1 axr -1 control 2 rx -1 rv[-1] ocontrol 2 rf 1 af 1              end_circuit 
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE OFNI_XE
	// left x boundary: v < 0:
	gate H axr[1] control_e rx 2  ocontrol 3 rf 1 af 1 rv[-1] end_gate

	// for x right boundary: v > 0:
	gate H axr 4  control rx -1           ocontrol 2 rf 1 af 1  ocontrol_e rv[-1]  end_gate  
	gate H axr 4  control 2 rv[-1] rx -1  ocontrol 2 rf 1 af 1                     end_gate  
	gate H axr 3  control rv[-1]          ocontrol 2 rf 1 af 1  ocontrol_e rx 2    end_gate 
END_CIRCUIT_STRUCTURE


// *** matrix F: axis v: bulk ***
CIRCUIT_STRUCTURE OF_VB
	// *** matrix Cf ***
	gate H av -1 control rf 1 ocontrol 2 af 1 rv -1 end_gate 
	// --- v: bulk velocity points:
	circuit Shpm 1 avr -1 ocontrol 3 af 1 rf 1 axr 3 end_circuit
END_CIRCUIT_STRUCTURE


// *** matrix F: axis v: edge ***
CIRCUIT_STRUCTURE OFN_VE
	// v: left boundary:
	circuit Shppp 1 avr -1                ocontrol 4 af 1 rf 1 axr 3 rv -1 end_circuit
	// v: right boundary:
	circuit Shmmm 1 avr -1 control rv -1  ocontrol 3 af 1 rf 1 axr 3       end_circuit
END_CIRCUIT_STRUCTURE



CIRCUIT_STRUCTURE OFNI_VE
	// left v boundary: 
	gate H avr[1] control_e rv 2 ocontrol 3 af 1 rf 1 axr 3  end_gate
	gate H avr[1] control_e rv 3 ocontrol 3 af 1 rf 1 axr 3  end_gate

	// right v boundary:
	gate H  avr[2] control rv -1 ocontrol 3 af 1 rf 1 axr 3                 end_gate
	gate H  avr 2                ocontrol 3 af 1 rf 1 axr 3 ocontrol_e rv 2 end_gate
	gate H  avr 2                ocontrol 3 af 1 rf 1 axr 3 ocontrol_e rv 3 end_gate
	gate Ry avr 4   1            ocontrol 3 af 1 rf 1 axr 3 ocontrol_e rv 2 end_gate
END_CIRCUIT_STRUCTURE



CIRCUIT_STRUCTURE U_BE_CE
	gate SIN a_ce 1 <alpha_0_CE> <alpha_1_CE> rv -1 end_gate
END_CIRCUIT_STRUCTURE



CIRCUIT_STRUCTURE OH
	// *** submatrix FF ***
	with ocontrol 2 af 1 rf 1 do
		file circuit_OH
	end_with

	// *** submatrix FP ***
	with ocontrol 2 af 1 rf 1 do
		// --- Main diag ---
		with ocontrol axr 3 do
			// *** left edge ***
			with ocontrol rv[-1] ocontrol rx -1 do
				gate H ae 1 end_gate
					gate SIN a_lcu 1 <a0_FB0_L> <a1_FB0_L> rv <Nvhm1> ocontrol ae 1 end_gate
					gate Rx  a_lcu 1 <a0_FB0_iw_L> control ae 1 end_gate
					gate X   a_lcu 1 control ae 1 end_gate
				gate H ae 1 end_gate
			end_with

			// -- corrections for the LEFT spatial edge --
			// >>> id_v = 0
			with ocontrol rv -1 ocontrol rx -1 do
				gate H ae 1 end_gate
					igate X   a_lcu 1                  control ae 1 end_gate
					igate Rx  a_lcu 1 <a0_FB0_iw_L>    control ae 1 end_gate
					gate Rx   a_lcu 1 <aL_corr_v0_i>   control ae 1 end_gate
					gate X    a_lcu 1                  control ae 1 end_gate
					igate X   a_lcu 1                 ocontrol ae 1 end_gate
					igate Ry  a_lcu 1 <aL_inv_v0>     ocontrol ae 1 end_gate
					gate  Ry  a_lcu 1 <aL_corr_v0_r>  ocontrol ae 1 end_gate
				gate H ae 1 end_gate
			end_with

			// >>> id_v = 2
			with control_e rv 2 ocontrol rx -1 do
				gate H ae 1 end_gate
					igate X   a_lcu 1                  control ae 1 end_gate
					igate Rx  a_lcu 1 <a0_FB0_iw_L>    control ae 1 end_gate
					gate Rx   a_lcu 1 <aL_corr_v2_i>   control ae 1 end_gate
					gate X    a_lcu 1                  control ae 1 end_gate
					igate X   a_lcu 1                 ocontrol ae 1 end_gate
					igate Ry  a_lcu 1 <aL_inv_v2>     ocontrol ae 1 end_gate
					gate  Ry  a_lcu 1 <aL_corr_v2_r>  ocontrol ae 1 end_gate
				gate H ae 1 end_gate
			end_with

			// >>> id_v = 3
			with control_e rv 3 ocontrol rx -1 do
				gate H ae 1 end_gate
					igate X   a_lcu 1                  control ae 1 end_gate
					igate Rx  a_lcu 1 <a0_FB0_iw_L>    control ae 1 end_gate
					gate Rx   a_lcu 1 <aL_corr_v3_i>   control ae 1 end_gate
					gate X    a_lcu 1                  control ae 1 end_gate
					igate X   a_lcu 1                 ocontrol ae 1 end_gate
					igate Ry  a_lcu 1 <aL_inv_v3>     ocontrol ae 1 end_gate
					gate  Ry  a_lcu 1 <aL_corr_v3_r>  ocontrol ae 1 end_gate
				gate H ae 1 end_gate
			end_with

			// *** right edge ***
			with control rv[-1] control rx -1 do
				gate H ae 1 end_gate
					gate SIN a_lcu 1 <a0_FB0_R> <a1_FB0_R> rv <Nvhm1> ocontrol ae 1 end_gate
					gate Rx  a_lcu 1 <a0_FB0_iw_R> control ae 1 end_gate
					gate X   a_lcu 1 control ae 1 end_gate
				gate H ae 1 end_gate
			end_with

			// -- corrections for the RIGHT spatial edge --
			// >>> id_v = Nv - 1
			with control_e rv <Nvm1> control rx -1 do
				gate H ae 1 end_gate
					igate X   a_lcu 1                   control ae 1 end_gate
					igate Rx  a_lcu 1 <a0_FB0_iw_R>     control ae 1 end_gate
					gate Rx   a_lcu 1 <aR_corr_vm1_i>   control ae 1 end_gate
					gate X    a_lcu 1                   control ae 1 end_gate
					igate X   a_lcu 1                  ocontrol ae 1 end_gate
					igate Ry  a_lcu 1 <aR_inv_vm1>     ocontrol ae 1 end_gate
					gate  Ry  a_lcu 1 <aR_corr_vm1_r>  ocontrol ae 1 end_gate
				gate H ae 1 end_gate
			end_with

			// >>> id_v = Nv - 3
			with control_e rv <Nvm3> control rx -1 do
				gate H ae 1 end_gate
					igate X   a_lcu 1                   control ae 1 end_gate
					igate Rx  a_lcu 1 <a0_FB0_iw_R>     control ae 1 end_gate
					gate Rx   a_lcu 1 <aR_corr_vm3_i>   control ae 1 end_gate
					gate X    a_lcu 1                   control ae 1 end_gate
					igate X   a_lcu 1                  ocontrol ae 1 end_gate
					igate Ry  a_lcu 1 <aR_inv_vm3>     ocontrol ae 1 end_gate
					gate  Ry  a_lcu 1 <aR_corr_vm3_r>  ocontrol ae 1 end_gate
				gate H ae 1 end_gate
			end_with

			// >>> id_v = Nv - 4
			with control_e rv <Nvm4> control rx -1 do
				gate H ae 1 end_gate
					igate X   a_lcu 1                   control ae 1 end_gate
					igate Rx  a_lcu 1 <a0_FB0_iw_R>     control ae 1 end_gate
					gate Rx   a_lcu 1 <aR_corr_vm4_i>   control ae 1 end_gate
					gate X    a_lcu 1                   control ae 1 end_gate
					igate X   a_lcu 1                  ocontrol ae 1 end_gate
					igate Ry  a_lcu 1 <aR_inv_vm4>     ocontrol ae 1 end_gate
					gate  Ry  a_lcu 1 <aR_corr_vm4_r>  ocontrol ae 1 end_gate
				gate H ae 1 end_gate
			end_with
		end_with
		// --- Left block FB1 ---
		with control_e axr 5 do
			gate SIN ae 1 <alpha_0_LFB1> <alpha_1_LFB1> rv -1 end_gate
			// - corr1 -
			igate SIN ae 1 <alpha_0_LFB1> <alpha_1_LFB1> rv -1 control_e rx 3 end_gate
			gate SIN ae 1 <alpha_0_LFB1_corr1> <alpha_1_LFB1_corr1> rv <Nvhm1> control_e rx 3 ocontrol rv[-1] end_gate
			gate SIN ae 1 <alpha_0_LFB1_corr1_2> <alpha_1_LFB1_corr1_2> rv <Nvhm1> control_e rx 3 control rv[-1] end_gate
			// - corr2 -
			igate SIN ae 1 <alpha_0_LFB1> <alpha_1_LFB1> rv -1 control_e rx <Nxm2> end_gate
			gate SIN ae 1 <alpha_0_LFB1_corr2> <alpha_1_LFB1_corr2> rv <Nvhm1> control_e rx <Nxm2> ocontrol rv[-1] end_gate
			gate SIN ae 1 <alpha_0_LFB1_corr2_2> <alpha_1_LFB1_corr2_2> rv <Nvhm1> control_e rx <Nxm2> control rv[-1] end_gate
			// - corr3 -
			igate SIN ae 1 <alpha_0_LFB1> <alpha_1_LFB1> rv -1 control_e rx -1 end_gate
			gate SIN ae 1 <alpha_0_LFB1_corr3> <alpha_1_LFB1_corr3> rv <Nvhm1> control_e rx -1 control rv[-1] end_gate
		end_with
		// --- Right block FB1 ---
		with control_e axr 1 do
			gate SIN ae 1 <alpha_0_RFB1> <alpha_1_RFB1> rv -1 end_gate
			// - corr1 -
			igate SIN ae 1 <alpha_0_RFB1> <alpha_1_RFB1> rv -1 control_e rx 0 end_gate
			gate SIN ae 1 <alpha_0_RFB1_corr1> <alpha_1_RFB1_corr1> rv <Nvhm1> control_e rx 0 ocontrol rv[-1] end_gate
			// - corr2 -
			igate SIN ae 1 <alpha_0_RFB1> <alpha_1_RFB1> rv -1 control_e rx 1 end_gate
			gate SIN ae 1 <alpha_0_RFB1_corr2> <alpha_1_RFB1_corr2> rv <Nvhm1> control_e rx 1  ocontrol rv[-1] end_gate
			gate SIN ae 1 <alpha_0_RFB1_corr2_2> <alpha_1_RFB1_corr2_2> rv <Nvhm1> control_e rx 1 control rv[-1] end_gate
			// - corr3 -
			igate SIN ae 1 <alpha_0_RFB1> <alpha_1_RFB1> rv -1 control_e rx <Nxm4> end_gate
			gate SIN ae 1 <alpha_0_RFB1_corr3> <alpha_1_RFB1_corr3> rv <Nvhm1> control_e rx <Nxm4> ocontrol rv[-1] end_gate
			gate SIN ae 1 <alpha_0_RFB1_corr3_2> <alpha_1_RFB1_corr3_2> rv <Nvhm1> control_e rx <Nxm4> control rv[-1] end_gate
			// - corr4 -
			igate SIN ae 1 <alpha_0_RFB1> <alpha_1_RFB1> rv -1 control_e rx <Nxm2> end_gate
			gate SIN ae 1 <alpha_0_RFB1_corr4> <alpha_1_RFB1_corr4> rv <Nvhm1> control_e rx <Nxm2> ocontrol rv[-1] end_gate
			gate SIN ae 1 <alpha_0_RFB1_corr4_2> <alpha_1_RFB1_corr4_2> rv <Nvhm1> control_e rx <Nxm2> control rv[-1] end_gate
		end_with
		// --- Block FL2 ---
		gate SIN ae 1 <alpha_0_FL2> <alpha_1_FL2> rv -1 control_e axr 2 end_gate
		// --- Block FR2 ---
		gate SIN ae 1 <alpha_0_FR2> <alpha_1_FR2> rv -1 control_e axr 6 end_gate
		gate Rz ae 1 <pi2> control_e axr 6 end_gate
	end_with

	// *** submatrix CF ***
	with control rf 1 ocontrol af 1  do
		gate SIN ae 1 <alpha_0_cf> <alpha_1_cf> av -1 end_gate
	end_with

	// *** submatrix CE ***
	with ocontrol rf 1 control af 1  do
		igate Rz ae 1  3.141592653589793 end_gate
		gate QSVT  vH  ae 1  U_BE_CE 2 a_ce 1 rv -1 end_gate
	end_with

	// *** submatrix S ***
	with control rf 1 control af 1  do
		gate Rx ae 1 <angle_sb>                end_gate
		gate Rx ae 1 <angle_se> ocontrol rv -1 end_gate
		gate X  ae 1                           end_gate
	end_with
END_CIRCUIT_STRUCTURE





CIRCUIT_STRUCTURE U_BE
	circuit  OF_VAR   7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	circuit  OF_XB    7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	circuit  OFN_XE   7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	circuit  OF_VB    7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	circuit  OFN_VE   7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	// --- comment the next line to compute the matrix D ---
	circuit  OH -1   end_circuit
	// ---
	circuit  OM       7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	// ---
	circuit  OFNI_VE  7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	icircuit OF_VB    7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	circuit  OFNI_XE  7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	icircuit OF_XB    7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
	icircuit OF_VAR   7 rv -1 rx -1 rf -1 av -1 avr -1 axr -1 af -1 end_circuit
END_CIRCUIT_STRUCTURE











