// $qucf kvn ./ 

CONSTANTS
	// Spatial resolution
	nx  7

	// --- SIN-gate: main diagonal ---
	alpha_x0_main 	-1.000000000000e+00
	alpha_x1_main 	1.007874015748e+00

	// --- SIN-gate: left diagonal ---
	alpha_x0_left 	-1.015748031496e+00
	alpha_x1_left 	1.015810031620e+00

	// --- SIN-gate: right diagonal ---
	alpha_x0_right 	-1.000000000000e+00
	alpha_x1_right 	1.015810031620e+00

	// --- Correcting angles ---
	ay_Am1_corr  -2.352911281153e-01
	az_Am1_corr  8.685491059009e-01

	ay_Ap1_corr  2.352911281153e-01
	az_Ap1_corr  8.685491059009e-01

	ay_Bm_m1_corr  1.004108183959e+02
	az_Bm_m1_corr  9.965704655406e+01

	ay_Bm_00_corr  5.439471657329e-01
	az_Bm_00_corr  4.029801688228e+00

	ay_Bm_p1_corr  1.004108183959e+02
	az_Bm_p1_corr  9.965704655406e+01

	//--- Angles for the weight-oracle ---
	nk 	7
	alpha_k0 	-1.000000000000e+00
	alpha_k1 	1.007874015748e+00

	// --- For the amplification ---
	N_AA_w        2

	// --- Time steps ---
	Nt 10
END_CONSTANTS


OPTIONS
    sel_compute_output zero-ancillae
	sel_print_output   none  // none, all, zero-ancillae
	flag_circuit 0 
	flag_tex     0
	tex_CL      6 
	flag_matrix 0
	flag_stop_gates    0
	flag_repeat_insert 1
END_OPTIONS


CIRCUITS_DECLARATION
	// --- 
	U_be_weights 2                       a_sin_k 1 1 rk <nk> 0
	Ow           3          a_qsvt_w 1 1 a_sin_k 1 1 rk <nk> 0 
	PREP         4 a_AA 1 1 a_qsvt_w 1 1 a_sin_k 1 1 rk <nk> 0
	RA           9 a_AA 1 1 a_qsvt_w 1 1 a_sin_k 1 1 rk <nk> 1 a_qsp 2 1 ae 1 1 asin 1 1 ax 2 1 rx <nx> 0 
	// --- for block-encoding LCHS matrices ---        
	Usin_main    2                                                              asin 1 1        rx <nx> 0
	Usin_left    2                                                              asin 1 1        rx <nx> 0
	Usin_right   2                                                              asin 1 1        rx <nx> 0
	U_BE_a       4                                                       ae 1 1 asin 1 1 ax 2 1 rx <nx> 0
	U_BE_kmax    4                                                       ae 1 1 asin 1 1 ax 2 1 rx <nx> 0 
	U_BE_k       4                                                       ae 1 1 asin 1 1 ax 2 1 rx <nx> 0 
	// --- for evolution in small time intervals ---
	U_QSP_k      5                                             a_qsp 2 1 ae 1 1 asin 1 1 ax 2 1 rx <nx> 0
	U_t_step     6                                   rk <nk> 1 a_qsp 2 1 ae 1 1 asin 1 1 ax 2 1 rx <nx> 0
	// --- the final circuit ---
	U            9 a_AA 1 1 a_qsvt_w 1 1 a_sin_k 1 1 rk <nk> 1 a_qsp 2 1 ae 1 1 asin 1 1 ax 2 1 rx <nx> 0 
END_CIRCUITS_DECLARATION


MAIN_CIRCUIT   U    
	INPUT_STATE  rx 0
END_MAIN_CIRCUIT

// --- For LCHS weights ---
CIRCUIT_STRUCTURE U_be_weights
	gate SIN a_sin_k 1 <alpha_k0> <alpha_k1> rk -1 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE  Ow
	gate H rk -1 end_gate
    gate QSVT  weights  a_qsvt_w 1  U_be_weights 2 a_sin_k 1 rk -1 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE   PREP
	circuit Ow 3 rk -1 a_sin_k 1 a_qsvt_w 1 end_circuit
	gate X a_AA 1 ocontrol 2 a_qsvt_w 1 a_sin_k 1 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE RA
	// sign change of the good state (state |1>)
	gate  Z a_AA 1 end_gate     
	//
	icircuit PREP 4 rk -1 a_sin_k 1 a_qsvt_w 1 a_AA 1 end_circuit
	// sign change of the initial state (state |0>|0>|0>):
	gate X a_AA 1 ocontrol 3 rk -1 a_sin_k -1 a_qsvt_w -1 end_gate
	gate Z a_AA 1 ocontrol 3 rk -1 a_sin_k -1 a_qsvt_w -1 end_gate
	gate X a_AA 1 ocontrol 3 rk -1 a_sin_k -1 a_qsvt_w -1 end_gate
	// 
	circuit PREP 4 rk -1 a_sin_k 1 a_qsvt_w 1 a_AA 1 end_circuit
END_CIRCUIT_STRUCTURE


// -----------------------------------------------------------------------
// --- SIN-gates ---
CIRCUIT_STRUCTURE Usin_main
	gate SIN asin 1 <alpha_x0_main> <alpha_x1_main> rx -1 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE Usin_left
	gate SIN asin 1 <alpha_x0_left> <alpha_x1_left> rx -1 end_gate
END_CIRCUIT_STRUCTURE


CIRCUIT_STRUCTURE Usin_right
	gate SIN asin 1 <alpha_x0_right> <alpha_x1_right> rx -1 end_gate
END_CIRCUIT_STRUCTURE


// -----------------------------------------------------------------------
// --- Block-Encoding oracle: A_a ---
CIRCUIT_STRUCTURE U_BE_a
	// --- OF_FORWARD ---
	gate H ax 1 end_gate
	gate H ax 1 ocontrol rx -1 end_gate
	gate H ax 1  control rx -1 end_gate
	gate X ax 1  control rx -1 end_gate
	// --- OH ---
	// left diagonal
	gate QSVT  A_m1  ae 1 Usin_left 2 asin 1 rx -1 control ax 1 end_gate
	
	// right diagonal
	gate QSVT  A_p1  ae 1 Usin_right 2 asin 1 rx -1 ocontrol ax 1 end_gate

	// --- correcting edges ---
	gate Rc ae 1 <az_Am1_corr> <ay_Am1_corr>  control 2 ax 1 rx -1 ocontrol asin 1 end_gate
	gate Rc ae 1 <az_Ap1_corr> <ay_Ap1_corr> ocontrol 2 ax 1 rx -1 ocontrol asin 1 end_gate
	// --- correcting the sign ---
	gate X ae 1 end_gate
	gate Z ae 1 end_gate
	gate X ae 1 end_gate
	// --- OM ---
	gate incrementor rx -1 ocontrol ax 1 end_gate
	gate decrementor rx -1 control  ax 1 end_gate
	// --- OF_BACKWARD ---
	gate H ax 1 end_gate
END_CIRCUIT_STRUCTURE


// -----------------------------------------------------------------------
// --- Block-Encoding oracle: B_kmax = - k_max_ * Ah_ ---
CIRCUIT_STRUCTURE U_BE_kmax
	// --- OF_FORWARD ---
	gate H ax 3 end_gate
	gate H ax[1] ocontrol rx -1 end_gate
	gate H ax[0]  control rx -1  end_gate

	// --- OH ---
	// left diagonal:
	gate QSVT  kmax_m1  ae 1 Usin_left  2 asin 1 rx -1 control_e ax[1] end_gate

	// main diagonal:
	gate QSVT  kmax_00    ae 1 Usin_main  2 asin 1 rx -1 ocontrol ax -1 end_gate
	gate QSVT  kmax_00_2  ae 1 Usin_main  2 asin 1 rx -1  control ax -1 end_gate

	// right diagonal:
	gate QSVT  kmax_p1  ae 1 Usin_right 2 asin 1 rx -1 control_e ax[0] end_gate
	
	// --- correcting edges ---
	// left diag
	gate Rc ae 1 <az_Bm_m1_corr> <ay_Bm_m1_corr> control_e ax[1]  control rx -1 end_gate

	// main diag
	gate Rc ae 1 <az_Bm_00_corr> <ay_Bm_00_corr> ocontrol ax -1  ocontrol rx -1 end_gate
	gate Rc ae 1 <az_Bm_00_corr> <ay_Bm_00_corr> ocontrol ax -1   control rx -1 end_gate
	gate Rc ae 1 <az_Bm_00_corr> <ay_Bm_00_corr>  control ax -1  ocontrol rx -1 end_gate
	gate Rc ae 1 <az_Bm_00_corr> <ay_Bm_00_corr>  control ax -1   control rx -1 end_gate

	// right diag
	gate Rc ae 1 <az_Bm_p1_corr> <ay_Bm_p1_corr> control_e ax[0] ocontrol rx -1 end_gate

	// --- correcting the sign ---
	gate X ae 1 end_gate
	gate Z ae 1 end_gate
	gate X ae 1 end_gate

	// --- OM ---
	gate incrementor rx -1 ocontrol ax[1] control ax[0] end_gate
	gate decrementor rx -1 ocontrol ax[0] control ax[1] end_gate
	// --- OF_BACKWARD ---
	gate H ax 3 end_gate
END_CIRCUIT_STRUCTURE


// -----------------------------------------------------------------------
// --- B_k ---
CIRCUIT_STRUCTURE   U_BE_k
	// --- Testing BE-oracle for the matrix Bk ---
	circuit U_BE_kmax -1 end_circuit

	// --- correct the sign ---
	gate X ae 1 end_gate
	gate Z ae 1 end_gate
	gate X ae 1 end_gate
END_CIRCUIT_STRUCTURE


// -----------------------------------------------------------------------
// --- QSP realization of the k-dependent part ---
CIRCUIT_STRUCTURE U_QSP_k
	gate QSVT qsp_k   a_qsp -1  U_BE_k 4 ae -1 asin -1 ax -1 rx -1 end_gate
END_CIRCUIT_STRUCTURE


// -----------------------------------------------------------------------
// --- One time step ---
CIRCUIT_STRUCTURE U_t_step
	// Aa:
	gate QSVT qsp_a     a_qsp -1  U_BE_a    4          ae -1 asin -1 ax -1 rx -1 end_gate

	// (- kmax*Ah):
	gate QSVT qsp_kmax  a_qsp -1  U_BE_kmax 4          ae -1 asin -1 ax -1 rx -1 end_gate

	// i_k * dk*Ah:
	gate SelectorPower  rk    -1  U_QSP_k   5 a_qsp -1 ae -1 asin -1 ax -1 rx -1 end_gate

	// Aa:
	gate QSVT qsp_a     a_qsp -1  U_BE_a    4          ae -1 asin -1 ax -1 rx -1 end_gate
END_CIRCUIT_STRUCTURE


// -----------------------------------------------------------------------
// --- Final circuit ---
CIRCUIT_STRUCTURE   U
	// initialization:
	gate H rx -1 end_gate

	// weights:
	circuit   PREP 4 rk -1 a_sin_k 1 a_qsvt_w 1 a_AA 1  end_circuit
	gate repeat RA 9 rx -1 ax -1 asin -1 ae -1 a_qsp -1 rk -1 a_sin_k 1 a_qsvt_w 1 a_AA 1  <N_AA_w> end_gate

	// Selector:
	gate repeat U_t_step 6 rk -1 a_qsp -1 ae -1 asin -1 ax -1 rx -1  <Nt> end_gate

	// inverse weights:
	igate repeat RA 9 rx -1 ax -1 asin -1 ae -1 a_qsp -1 rk -1 a_sin_k 1 a_qsvt_w 1 a_AA 1  <N_AA_w> end_gate
	icircuit   PREP 4 rk -1 a_sin_k 1 a_qsvt_w 1 a_AA 1  end_circuit
END_CIRCUIT_STRUCTURE


